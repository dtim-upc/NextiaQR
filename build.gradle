plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'edu.upc.essi.dtim'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.jena', name: 'apache-jena-libs', version: '4.1.0', ext: 'pom'
    implementation group: 'org.apache.jena', name: 'jena-querybuilder', version: '4.1.0'

    implementation group: 'net.minidev', name: 'json-smart', version: '2.4.7'
    implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    implementation group: 'com.google.guava', name: 'guava', version: '31.0-jre'

    implementation 'org.jgrapht:jgrapht-core:1.2.0'

    //implementation 'org.apache.spark:spark-core_2.11:2.4.3'
    //implementation 'org.apache.spark:spark-sql_2.11:2.4.3'

    //Javier: check difference of spark-core_2.12 vs spark-core_2.13
    implementation group: 'org.apache.spark', name: 'spark-core_2.12', version: '3.2.1'
    implementation group: 'org.apache.spark', name: 'spark-sql_2.12', version: '3.2.1'

    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.6'
    implementation group: 'postgresql', name: 'postgresql', version: '9.1-901-1.jdbc4'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '5.1.44'
    implementation group: 'com.opencsv', name: 'opencsv', version: '4.0'
    implementation group: 'org.mongodb', name: 'mongo-java-driver', version: '3.12.10'

    //compile files('lib/sqlite4java-392/sqlite4java.jar')
    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0.3'


    //testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}


test {
    useJUnitPlatform()
}
//task customFatJar(type: Jar) {
//    manifest {
//        attributes 'Main-Class': 'edu.upc.essi.dtim.Nuupdi'
//    }
//    baseName = 'all-in-one-jar'
//    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
//    with jar
//}
task uberJar(type: Jar) {
    archiveClassifier = 'uber'
    zip64(true)

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}



signing {
    required {
        // signing is required if this is a release version and the artifacts are to be published
        !version.toString().endsWith('-SNAPSHOT') && tasks.withType(PublishToMavenRepository).find {
            gradle.taskGraph.hasTask it
        }
    }
    sign publishing.publications
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'NextiaDI'
                description = 'A query rewriting algorithm'
                url = 'https://www.essi.upc.edu/dtim/nextiadi/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'snadal'
                        name = 'Sergi Nadal'
                        email = 'snadal@essi.upc.edu'
                        url = 'https://www.essi.upc.edu/dtim/people/snadal'
                    }
                    developer {
                        id = 'jflores'
                        name = 'Javier Flores'
                        email = 'jflores@essi.upc.edu'
                        url = 'https://www.essi.upc.edu/dtim/people/jflores'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/dtim-upc/NextiaDI.git'
                    developerConnection = 'scm:git:ssh://github.com/dtim-upc/NextiaDI.git'
                    url = 'https://github.com/dtim-upc/NextiaDI'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                // nexusUsername and nexusPassword are in gradle.properties file.
                username nexusUsername
                password nexusPassword
            }

            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}